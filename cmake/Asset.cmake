#===============================================================================
#
# CMake script for generating .s files from binary assets
#
# Copyright (C) 2021-2023 gba-toolchain contributors
# For conditions of distribution and use, see copyright notice in LICENSE.md
#
#===============================================================================

if(NOT INPUTS OR NOT OUTPUT)
    return()
endif()

function(split var size)
    string(LENGTH ${${var}} len)

    set(chunks)
    foreach(i RANGE 0 ${len} ${size})
        string(SUBSTRING ${${var}} ${i} ${size} chunk)
        list(APPEND chunks ${chunk})
    endforeach ()

    set(${var} ${chunks} PARENT_SCOPE)
endfunction()

file(WRITE "${OUTPUT}" "/* Generated by Asset.cmake */")

foreach(arg ${INPUTS})
    file(READ "${arg}" data HEX)
    string(LENGTH ${data} size)
    split(data 32)

    get_filename_component(arg "${arg}" NAME)
    string(REGEX REPLACE "[^a-zA-Z0-9_]" "_" arg "${arg}")
    string(PREPEND arg "${PREFIX}")

    file(APPEND "${OUTPUT}"  "
    .section .rodata.${arg}, \"a\"
    .balign
    .global ${arg}
${arg}:
    ")

    foreach(line ${data})
        split(line 2)
        list(TRANSFORM line PREPEND "0x")
        list(JOIN line ", " line)

        file(APPEND "${OUTPUT}" ".byte ${line}
    ")
    endforeach()

    math(EXPR size "${size} / 2")

    file(APPEND "${OUTPUT}" "
    .global ${arg}_end
${arg}_end:

    .global ${arg}_size
    .balign 4
${arg}_size: .int ${size}
")
endforeach()
